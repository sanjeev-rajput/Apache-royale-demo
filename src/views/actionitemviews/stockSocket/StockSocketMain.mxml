<j:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:j="library://ns.apache.org/royale/jewel"
        xmlns:js="library://ns.apache.org/royale/basic"
        xmlns:sp="library://ns.unhurdle.com/spectrum" 
        initComplete="init()"
        width="100%" height="100%">

    <j:beads>
        <js:ContainerDataBinding/>
    </j:beads>

    <fx:Script>
        <![CDATA[
            import views.actionitemviews.websocket.SocketService;
            import com.util.DictionaryUtil;
            import org.apache.royale.events.Event;
            import com.unhurdle.spectrum.Switch;
            import org.apache.royale.events.MouseEvent;
            import com.unhurdle.spectrum.Radio;
            import views.actionitemviews.stockSocket.StockItemG;
            import views.actionitemviews.stockSocket.StockItemT;
            import org.apache.royale.jewel.Group;
            import com.event.DsEvent;
            import com.unhurdle.spectrum.data.MenuItem;

            private var _socketService:SocketService;
            private var itemMap:DictionaryUtil = new DictionaryUtil();
            private var _recordSets:int = 100;
            [Bindable] private var _actionStat:Boolean = false;
            private var _socketItemCounter:int = 1;
            private static const GRID:String = "grid";
            private static const TILE:String = "tile";
            private var _layoutType:String = GRID;

            private function init():void {
                _socketService = new SocketService();
                _socketService.addCAllBackFunction(onSocketData);
            }

            private function onSocketData(data:Object):void {
                if (data.type == "stock_update") {
                    for each (var item:Object in data.payload) {
                        checkAndAddStockItem(item);
                    }
                }
                if(data.type == "error"){
                    _actionStat = false;
                    var sw:Switch = this["switchBtn"+_recordSets] as Switch;
                    sw.checked = false;
                }
            }

            private function checkAndAddStockItem(d:Object):void {
                var existingItem:* = itemMap.get(d.symbol);
                if (existingItem) {
                    existingItem.sData = d;
                    return;
                }

                var sItem:* = (_layoutType == GRID) ? new StockItemG() : new StockItemT();
                sItem.sData = d;
                sItem.iIndex = _socketItemCounter++;
                
                var container:Group = (_layoutType == GRID) ? sGitemCtr : sTitemCtr;
                container.addElement(sItem);
                //container.element.scrollTop = container.element.scrollHeight;

                itemMap.set(d.symbol, sItem);
            }

            private function dataPickerChangeHandler(e:Event):void {
                var btn:MenuItem = MenuItem(e.target);
                if (btn == switchBtn100) _recordSets = 100;
                if (btn == switchBtn500) _recordSets = 500;
                if (btn == switchBtn1000) _recordSets = 1000;
            }

            private function showHideHistoryData(e:Event):void {
                 DsEvent.instance.dispatch(DsEvent.HISTORIC_DATA, Switch(e.target).checked);
            }

            private function startStopSocketConnection(startIt:Boolean):void {
                if (startIt) {
                    clearStockUI();
                    if(dataSetPicker.selectedIndex == -1) dataSetPicker.selectedIndex = 0;
                    _socketService.connectWebSocket(SocketService.SUBSCRIBE_STOCK, { length: _recordSets });
                    _actionStat = true;
                } else {
                    _socketService.sendToSocket({ type: "unsubscribe_stock" });
                    _actionStat = false;
                }
            }

            private function layoutEventHandler(e:MouseEvent):void {
                var rBtn:Radio = Radio(e.target);
                var newLayout:String = (rBtn == gridV) ? GRID : TILE;

                if (_layoutType == newLayout) return;
                _layoutType = newLayout;

                sGitemCtr.visible = (_layoutType == GRID);
                sTitemCtr.visible = (_layoutType == TILE);
                switchHistoryOnOff.disabled = (newLayout == TILE);
                switchHistoryOnOff.checked = false;
                clearStockUI();
                
            }

            private function clearStockUI():void {
                itemMap.clear();
                _socketItemCounter = 1;
                var clearContainer:Group = (_layoutType == GRID) ? sGitemCtr:sTitemCtr ;
                while (clearContainer.numElements) {
                    clearContainer.removeElement(clearContainer.getElementAt(0));
                }
            }

            public function disposeMe():void {
                _socketService.sendToSocket({ type: "unsubscribe_stock" });
                _socketService.disconnectWebSocket();
            }
        ]]>
    </fx:Script>

    <j:HGroup width="100%" gap="20" className="stockControls" itemsHorizontalAlign="itemsCenter">
        <sp:Radio id="gridV" radioName="viewType" text="Grid" checked="true" disabled="{_actionStat}" click="layoutEventHandler(event)"/>
        <sp:Radio id="tileV" radioName="viewType" text="Tile" disabled="{_actionStat}" click="layoutEventHandler(event)"/>

        <sp:Picker id="dataSetPicker" placeholder="Select data set" change="dataPickerChangeHandler(event)" disabled="{_actionStat}">
            <sp:dataProvider>
                <fx:Array>
                    <sp:MenuItem id="switchBtn100" text="100 Data Set"/>
                    <sp:MenuItem id="switchBtn500" text="500 Data Set"/>
                    <sp:MenuItem id="switchBtn1000" text="1000 Data Set"/>
                </fx:Array>
            </sp:dataProvider>
        </sp:Picker>
        <j:Spacer width="20"/>
        <sp:Switch id="switchHistoryOnOff" offLabel="off" onLabel="on " checked="false" change="showHideHistoryData(event)">
            <sp:beads>
                <sp:TooltipBead toolTip="Show/Hide Historic Data"/>
            </sp:beads>
        </sp:Switch>
        <j:ToggleButton click="startStopSocketConnection(!_actionStat)" selected="{_actionStat}">
            <j:icon>
                <js:MaterialToggleIcon text="{MaterialIconType.START}" selectedText="{MaterialIconType.STOP}" />
            </j:icon>
            <j:beads>
                <j:ToolTip toolTip="start/stop socket connection"/>
            </j:beads>
        </j:ToggleButton>

    </j:HGroup>

    <j:VGroup id="sGitemCtr" width="100%" height="100%">
        <j:beads>
            <j:ScrollingViewport/>
        </j:beads>
    </j:VGroup>

    <j:Group id="sTitemCtr" height="100%" width="100%" style="padding:10px" visible="false">
        <j:beads>
            <j:TileHorizontalLayout localId="plist" waitForSize="true" itemsHorizontalAlign="itemsSpaceAround"/>
            <j:ScrollingViewport/>
        </j:beads>
    </j:Group>

</j:VGroup>
